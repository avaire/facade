plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id 'com.gorylenko.gradle-git-properties' version '1.4.17'
}

apply plugin: 'java'
apply plugin: 'idea'

version '0.1'
group 'com.avairebot'
description = 'AvaIre API facade'
mainClassName = 'com.avairebot.facade.Main'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    moduleName = 'Facade'
}

shadowJar {
    archiveName = "Facade.jar"

    doLast {
        copy {
            from 'build/libs/Facade.jar'
            into '.'
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile group: 'com.avairebot', name: 'Shared', version: '6405bfa912'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.1'
    compile group: 'com.google.guava', name: 'guava', version: '20.0'
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.19'
    compile group: 'com.sparkjava', name: 'spark-core', version: '2.7.2'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filesMatching("**/app.properties") {
        filter ReplaceTokens, tokens: [
                "project.version"   : project.version,
                "project.groupId"   : project.group,
                "project.artifactId": project.ext.moduleName
        ]
    }
}
